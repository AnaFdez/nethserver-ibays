#!/usr/bin/perl 

#
# nethserver-ibays-set-password -- change the ibay password with htpasswd
#
# Copyright (C) 2012 Nethesis srl
#

use strict;

use esmith::AccountsDB;

my $eventName = shift || die('Missing event name argument!');
my $givenIbay = shift;

my $htpasswd = '/usr/bin/htpasswd';
my $passwordFile = '/etc/httpd/conf/ibays.htpasswd';
my $errors = 0;

if ( ! -x $htpasswd ) {
    # If htpasswd executable is missing quit here.
    exit 0;
}

my $db = esmith::AccountsDB->open_ro() || die('Could not open accounts database!');
my @ibays = $givenIbay ? ($db->get($givenIbay)) : $db->ibays;

foreach my $ibayRecord (@ibays) {

    my $ibayName = $ibayRecord->key;

    # Skip if no password is required
    if($ibayRecord->prop('HttpPasswordStatus') ne 'enabled') {
	next;
    }

    my $password = $ibayRecord->prop('HttpPasswordValue');

    # Error, if no passowd is set
    if( ! $password) {
	warn "Password has not been set in HttpPasswordValue, for ibay $ibayName\n";
	$errors ++;
	next;
    }

    my $action = (-r $passwordFile ? 'update' : 'create');
    if(system($htpasswd, 
	      '-b' . ($action eq 'create' ? 'c' : '') . 's', 
	      $passwordFile, 
	      $ibayName, 
	      $password) != 0) {
	warn("Could not $action the htpasswd database file in " . $passwordFile);
	$errors ++;
    }
}

exit ($errors == 0 ? 0 : 1);
